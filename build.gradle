//
//  Author:  github.com/luncliff (luncliff@gmail.com)
//  Note
//      See https://github.com/luncliff/Muffin for the detail
//
buildscript {    
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.1'
    }
}

repositories {
    google()
    jcenter()
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion 27

    // Target ABI
    //  - This option controls target platform of module
    //  - The platform might not supported by the compiler
    //    some can work with Clang(default), but some can work only with GCC...
    //    if bad, both toolchains might not support it
    splits {
        abi {
            // disable all
            enable true
            universalApk true 
            reset()
            // Build for multiple platform, like...
            include "armeabi-v7a",  "arm64-v8a", "x86_64"
        }
    }

    defaultConfig {
        minSdkVersion 24        // Minimum SDK
        targetSdkVersion 27

        // Follow native code's verson
        versionCode 150
        versionName "1.5.0"

        // External Native build: CMake
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_shared"
            }
        }
    }
    // External Native build
    //  - using Gradle's CMake plugin
    //  - Give path to CMake
    externalNativeBuild {
        cmake {
            // it's ok to use 3.8 or later, 
            // but specify the version since this gradle requires it
            // see 'scripts/install-cmake-3.14.sh'
            // version "3.14.5"
            path "CMakeLists.txt"
        }
    }
    
    // Pick first one from duplicated shared objects.
    // https://google.github.io/android-gradle-dsl/current/com.android.build.gradle.internal.dsl.PackagingOptions.html
    // https://stackoverflow.com/questions/44342455/more-than-one-file-was-found-with-os-independent-path-meta-inf-license
    packagingOptions {
        pickFirst 'lib/**'
    }

    // Custom SourceSet
    //  - Source path/directory for Gradle
    sourceSets {
        // Command: gradle assemble
        main.setRoot('android')
        main {
            manifest.srcFile 'android/AndroidManifest.xml'
        }

        // If ABI includes `armeabi-v7a`, the following directory MUST exists.
        //      `android/jniLibs/armeabi-v7a`
        //      `libs/armeabi-v7a`
        // The library files in there will be installed automatically
        main.jniLibs.srcDirs = ['android/jniLibs', 'libs']
    }
}

assemble.doLast
{
    // Instead of `ninja install`, Gradle will deploy the files. 
    copy {
        from 'build/intermediates/cmake/debug/obj'
        into 'install/libs/Debug'
    }
    copy {
        from 'build/intermediates/cmake/release/obj'
        into 'install/libs/Release'
    }
    copy {
        from 'interface'
        into 'install/include'
    }
}
